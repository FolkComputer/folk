Wish $this has filename "with-all.folk"

set ::collectedMatches [dict create]
When when the collected matches for /clause/ are /matches/ /body/ with environment /e/ {
    set varNames [lmap word $clause {expr {
        [regexp {^/([^/ ]+)/$} $word -> varName] ? $varName : [continue]
    }}]
    When {*}$clause {
        set match [dict create]
        foreach varName $varNames { dict set match $varName [set $varName] }

        dict set ::collectedMatches $clause $match true
        On unmatch { dict unset ::collectedMatches $clause $match }
    }
    When $::nodename has step count /c/ {
        set matchId $::matchId
        Before convergence {
            if {[dict exists $Statements::matches $matchId] &&
                [dict exists $::collectedMatches $clause]} {
                set oldMatchId $::matchId; set ::matchId $matchId
                set matches [dict get $::collectedMatches $clause]
                Say the collected matches for $clause are [dict keys $matches]
                set ::matchId $oldMatchId
            }
        }
    }
    On unmatch { dict unset ::collectedMatches $clause }
}
