When when /rect/ points /direction/ at /someone/ /lambda/ with environment /e/ {
  Wish $rect points $direction
}

When /someone/ wishes /rect/ points /direction/ & /rect/ has region /region/ {
    if {$direction eq "up"} {
        set whiskerRegion [region move [region scale $region width 0.01px] up 50%]
        set color blue
    } elseif {$direction eq "left"} {
        set whiskerRegion [region move [region scale $region height 0.01px] left 50%]
        set color red
    } elseif {$direction eq "right"} {
        set whiskerRegion [region move [region scale $region height 0.01px] right 50%]
        set color green
    } elseif {$direction eq "down"} {
        set whiskerRegion [region move [region scale $region width 0.01px] down 50%]
        set color white
    }
    set whisker [list $rect whisker $direction]
    Claim $whisker has region $whiskerRegion
    Wish $whisker is outlined $color

    When /target/ has region /r2/ {
        if {$target != $rect && $target != $whisker && \
                [region intersects $whiskerRegion $r2]} {
            Claim $rect points $direction at $target
        }
    }
}
