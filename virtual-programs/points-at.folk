When when /rect/ points /direction/ with length /l/ at /someone/ /lambda/ with environment /e/ {
  Wish $rect points $direction with length $l
}

When when /rect/ points /direction/ at /someone/ /lambda/ with environment /e/ {
  Wish $rect points $direction with length 1
}

When /someone/ wishes /rect/ points /direction/ with length /l/ & /rect/ has region /region/ {

    set scale [expr {$l * 100}]%
    set shift 90%

    if {$direction eq "up"} {
        set whiskerRegion [region move [region scale [region scale $region width 0.01px] height $scale] up $shift]
        set color blue
    } elseif {$direction eq "left"} {
        set whiskerRegion [region move [region scale [region scale $region height 0.01px] width $scale] left $shift]
        set color red
    } elseif {$direction eq "right"} {
        set whiskerRegion [region move [region scale [region scale $region height 0.01px] width $scale] right $shift]
        set color green
    } elseif {$direction eq "down"} {
        set whiskerRegion [region move [region scale [region scale $region width 0.01px] height $scale] down $shift]
        set color white
    }
    set whisker [list $rect whisker $direction]
    Claim $whisker has region $whiskerRegion
    Wish $whisker is outlined $color

    When /target/ has region /r2/ {
        if {$target != $rect && $target != $whisker && \
                [region intersects $whiskerRegion $r2]} {
            Claim $rect points $direction at $target
            Claim $rect points $direction with length $l at $target
        }
    }
}
