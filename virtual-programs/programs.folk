When tag /tag/ has detection /any/ on camera /any/ at timestamp /any/ {
    # Setting aside this tag space (48600 to 48713) for calibration.
    if {$tag >= 48600} { return }

    Claim (keep 100ms) tag $tag is a tag
    Claim (keep 100ms) tag $tag has a program
}

# Setting aside this tag space (45000 to 45050) for Folk Demo Booklet
Claim 45000 has program code "Wish \$this is labelled \"Welcome to Folk! This is a program\"
    Wish \$this is outlined green"
Claim 45001 has program code "Wish \$this is labelled \"My wishes came true!\"
    Wish \$this is outlined blue"
Claim 45002 has program code "When /actor/ is cool \{
    Wish \$this is labelled \"\$actor is pretty cool\"
    Wish \$actor is outlined red \}"
Claim 45003 has program code "Claim \$this is actor
    Claim \$this is cool"
Claim 45004 has program code "When the clock time is /t/ \{
    Wish \$this is labelled \$t\}"
Claim 45005 has program code "When the clock time is /t/ \{
    Wish \$this draws a circle offset \[list expr \{sin\(\$t\) * 50\} 0\] \}"
Claim 45006 has program code "Wish \$this is outlined blue
    When \$this points up at /p/ \{
        Wish \$this is labelled \"Pointing at \$p\"
        Wish \$p is labelled \"and I am being pointed at\"\}"
Claim 45007 has program code "Wish \$this is outlined red
    Wish \$this is labelled \"I am a program\""
Claim 45008 has program code "When \$this has camera slice /slice \{
    Wish \$this displays camera slice \$slice \}"


When -noncapturing the program save directory is /saveDir/ {
    When /type/ /obj/ has a program {
        puts stderr "Added $type $obj"
        On unmatch { puts "Removed $type $obj" }

        if {$obj >= 45000} { return }

        try {
            if {[file exists "$saveDir/$obj.folk.temp"]} {
                set fd [open "$saveDir/$obj.folk.temp" r]
            } else {
                if {![file exists "$saveDir/$obj.folk"] &&
                    ($::thisNode eq "folk-beads" || $::thisNode eq "folk-convivial" || $::thisNode eq "gadget-platinum")} {
                    # HACK: 'Page fault' to folk0, try getting page from
                    # there. Ideally we would have some general (Avahi?)
                    # way of finding the 'authoritative' node on the local
                    # network, or broadcasting out, and getting pages from
                    # there.
                    exec curl --output "$saveDir/$obj.folk" \
                        "http://folk0.local:4273/printed-programs/$obj.folk" &
                    exec curl --output "$saveDir/$obj.meta.folk" \
                        "http://folk0.local:4273/printed-programs/$obj.meta.folk" &
                    # HACK: It won't be reloaded until you redetect the tag.
                }
                set fd [open "$saveDir/$obj.folk" r]
            }
            set code [read $fd]
            close $fd

            Claim $obj has program code $code

            if {[file exists "$saveDir/$obj.meta.folk"]} {
                set mfd [open "$saveDir/$obj.meta.folk" r]
                set metacode [read $mfd]; close $mfd
                apply [list {this} $metacode] $obj
            }
        } on error err {
            puts stderr "No code for $type $obj ($err):\n  [errorInfo $err [info stacktrace]]"
        }
    }
}
