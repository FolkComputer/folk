When the db library is /dbLib/ {
  Wish the web server handles route "/" with handler [list apply {{db dbLib} {
    set l [list]
    # You need to use the Simple form so it doesn't auto-claimize it.
    set results [QuerySimple! false /...terms/]
    foreach result $results { dict with result {
        set childMatchRefs [$dbLib childMatches $db $__ref]
        set childMatchInfo [join [lmap childMatchRef $childMatchRefs {
            set childStatementRefs [$dbLib childStatements $db $childMatchRef]
            subst {
                \[ $childMatchRef ($childStatementRefs) \]
            }
        }] " "]

        lappend l [subst {
            <li>
            <details>
            <summary style="[expr {
              [lsearch -exact $terms error] != -1
              ? "color: red"
              : ""}]">
            $__ref ([$dbLib statementParentCount $db $__ref]): <code>[htmlEscape [string range $terms 0 100]]</code> ($childMatchInfo)</summary>
            <pre>[htmlEscape $terms]</pre>
            </details>
            </li>
        }]
    } }

    # Generate navigation from all route handlers.
    set handlers [Query! /someone/ wishes the web server handles route /route/ with /...options/]
    set navLinks [list]
    foreach handler $handlers { dict with handler {
      if {[dict getdef $options hidden false]} { continue }
      if {$route eq "/"} { continue }

      # Create a nice display name from the route.
      set displayRoute [regsub {\$$} $route ""]
      set displayName [string trim $displayRoute "/"]
      set displayName [string map {"-" " " ".pdf" "" "_" " "} $displayName]
      set displayName [string totitle $displayName]

      # Use the original route for the href.
      set cleanRoute [regsub {\$\s*$} $route ""]

      set nav [dict getdef $options nav $displayName]
      lappend navLinks "<a href=\"$cleanRoute\">$nav</a>"
    } }

    html [subst {
        <html>
        <head>
        <link rel="stylesheet" href="/style.css">
        <title>Statements</title>
        </head>
        <nav>
          [join $navLinks "\n          "]
        </nav>
        <h1>Statements</h1>
        <ul>[join $l "\n"]</ul>
        </html>
    }]
  }} [__db] $dbLib]
}
