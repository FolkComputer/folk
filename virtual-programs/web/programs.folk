Wish the web server handles route "/programs" with handler {
    proc emitHTMLForProgramList {programList label} {
        set prettyLabel [string map {- " "} $label]
        set prettyLabel [string totitle $prettyLabel]:
        set returnList [list "<details data-label='$label' data-count='[llength $programList]'><summary>$prettyLabel ([llength $programList])</summary>"]
        lappend returnList "<ul>"
        foreach item $programList {
            lappend returnList "<li><details><summary>[dict get $item programName]</summary><pre><code>[htmlEscape [dict get $item programCode]]</code></pre></details></li>"
        }
        lappend returnList "</ul>"
        lappend returnList "</details>"
        join $returnList
    }

    set programs [Query! /someone/ claims /programName/ has program code /programCode/]
    set vp [list]; # virtual programs
    set cp [list]; # core programs
    set wp [list]; # web programs
    set rp [list]; # real programs

    foreach match $programs {
        set programName [dict get $match programName]
        switch -glob $programName {
            "virtual-programs/*" {
                lappend vp $match
            }
            "/home/*" {
                lappend cp $match
            }
            "web-program-*" {
                lappend wp $match
            }
            default {
                lappend rp $match
            }
        }
    }

    html [subst {
        <html>
        <head>
            <title>Running programs</title>
            <link rel="stylesheet" href="/style.css">
            <style>
                body {
                    font-family: math;
                }
                summary {
                    font-family: monospace;
                    font-size: 2em;
                }
            </style>
            <script src="/lib/folk.js"></script>
            <script>
            /* TODO:
                (  ) Add a ws.watch() for /someone/ claims /programName/ has program /program/
            */
            </script>
        </head>
        <body>
            [emitHTMLForProgramList $rp "real-programs"]
            [emitHTMLForProgramList $wp "web-programs"]
            [emitHTMLForProgramList $vp "virtual-programs"]
            [emitHTMLForProgramList $cp "core-programs"]
            [expr {[llength $rp] ?  "<h2>[llength $rp] [expr {[llength $rp] == 1 ? "program is" : "programs are"}] out.</h2>" : "No real programs are out."}]
        </body>
        </html>
    }]
}
