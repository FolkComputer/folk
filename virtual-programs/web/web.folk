puts Web

proc handleConnect {chan addr} {
    fileevent $chan readable [list handleRead $chan $addr]
}

proc htmlEscape {s} { string map {& "&amp;" < "&lt;" > "&gt;" "\"" "&quot;"} $s }

proc readFile {filename contentTypeVar} {
    upvar $contentTypeVar contentType
    set fd [open $filename r]
    fconfigure $fd -encoding binary -translation binary
    set response [read $fd]; close $fd; return $response
}

proc getDotAsPdf {dot contentTypeVar} {
    upvar $contentTypeVar contentType
    set contentType "application/pdf"
    set fd [open |[list dot -Tpdf <<$dot] r]
    fconfigure $fd -encoding binary -translation binary
    set response [read $fd]; close $fd; return $response
}

proc handlePage {path httpStatusVar contentTypeVar} {
    upvar $contentTypeVar contentType
    switch -exact -- $path {
        "/favicon.ico" {
            set contentType "image/x-icon"
            readFile "assets/favicon.ico" contentType
        }
        "/style.css" {
            set contentType "text/css"
            readFile "assets/style.css" contentType
        }
        "/statementClauseToId.pdf" {
            getDotAsPdf [trie dot [Statements::statementClauseToIdTrie]] contentType
        }
        "/statements.pdf" {
            getDotAsPdf [Statements::dot] contentType
        }
        default {
            upvar $httpStatusVar httpStatus
            set httpStatus "HTTP/1.1 404 Not Found"
            subst {
                <html>
                <b>$path</b> Not found.
                </html>
            }
        }
    }
}

proc handleRead {chan addr} {
    gets $chan line; set firstline $line
    puts "Http: $chan $addr: [string range $line 0 end-1]"
    set headers [list]
    while {[gets $chan line] >= 0 && $line ne ""} {
        # puts "  Http: ([string range $line 0 end-1])"
        if {[regexp {^([^\s:]+)\s*:\s*(.+)} $line -> k v]} {
            lappend headers $k $v
        } else { break }
    }

    if {[regexp {GET ([^ ]*) HTTP/1.1} $firstline -> path]} {
        set response {}
        set handlers [Query! /someone/ wishes the web server handles route /route/ with handler /handler/]
        try {
            foreach h $handlers {
                set route [dict get $h route]
                set handler [dict get $h handler]
                if {[regexp ^$route$ $path whole_match]} {
                    set ^html [proc html {body} {dict create statusAndHeaders "HTTP/1.1 200 OK\nConnection: close\nContent-Type: text/html; charset=utf-8\n\n" body $body}]
                    set ^json [proc json {body} {dict create statusAndHeaders "HTTP/1.1 200 OK\nConnection: close\nContent-Type: application/json; charset=utf-8\n\n" body $body}]
                    set response [apply [list {path} $handler] $path]
                }
            }
            if {$response eq ""} {
                set httpStatus "HTTP/1.1 200 OK"
                set contentType "text/html; charset=utf-8"
                set body [handlePage $path httpStatus contentType]
                set response [dict create statusAndHeaders "$httpStatus\nConnection: close\nContent-Type: $contentType\n\n" body $body]
            }
            if {![dict exists $response statusAndHeaders]} {
                error "Response not generated"
            }
        } on error e {
            set contentType "text-html; charset=utf-8"
            set body [subst {
                <html>
                <head>
                <title>folk: 500 Internal Server Error</title>
                </head>
                <body>
                <pre>[htmlEscape $e]:
[htmlEscape [errorInfo $e [info stacktrace]]]</pre>
                </body>
                </html>
            }]
            set response [dict create statusAndHeaders "HTTP/1.1 500 Internal Server Error\nConnection: close\nContent-Type: $contentType\n\n" body $body]
        }
        puts -nonewline $chan [dict get $response statusAndHeaders]
        if {[dict exists $response body]} {
            puts -nonewline $chan [dict get $response body]
        }
        close $chan

    } elseif {[regexp {POST ([^ ]*) HTTP/1.1} $firstline -> path]} {
        set httpStatus "HTTP/1.1 200 OK"
        set contentType "text/plain; charset=utf-8"
        puts -nonewline $chan "$httpStatus\r\nConnection: close\r\nContent-Type: $contentType\r\n\r\n"

        set body [$chan read [dict get $headers Content-Length]]
        puts "  ($body)"
        try {
            puts -nonewline $chan [eval $body]
        } on error e {
            puts -nonewline $chan "Error: $e"
        }
        close $chan

    } else { puts "Closing: $chan $addr $headers"; close $chan }
}

set f [socket stream.server 4273]
$f readable [lambda {} {f} {
    set client [$f accept addr]
    handleConnect $client $addr
}]
vwait forever

