set threadMonitorLib [apply {{} {
    set cc [C]
    $cc cflags -I.
    $cc include "workqueue.h"
    $cc include "common.h"
    $cc code {
        extern ThreadControlBlock threads[];
        extern int _Atomic threadCount;

        extern Db* db;
    }
    $cc proc workerInfo {int tid} char* {
        static char buf[1000];
        char *s = buf;
        #define EMIT(...) s += snprintf(s, sizeof(buf) - (s - buf), __VA_ARGS__)
        for (int i = 0; i < threadCount; i++) {
            if (threads[i].tid != tid) { continue; }

            WorkQueueItem item = threads[i].currentItem;
            if (item.op == ASSERT) {
                EMIT("Assert");
            } else if (item.op == RETRACT) {
                EMIT("Retract");
            } else if (item.op == HOLD) {
                EMIT("Hold");
            } else if (item.op == SAY) {
                EMIT("Say");
            } else if (item.op == RUN) {
                Statement* stmt = statementAcquire(db, item.run.stmt);
                if (stmt != NULL) {
                    EMIT("Run when (%.100s) (%.100s)",
                         clauseToString(item.run.whenPattern),
                         clauseToString(statementClause(stmt)));
                    statementRelease(db, stmt);
                }
            } else if (item.op == REMOVE_PARENT) {
                EMIT("Remove Parent");
            } else {
                EMIT("???");
            }
            EMIT(threads[i].isAwaitingPush ? " (Awaiting Push)" : "");
            return buf;
        }
        #undef EMIT
        return "<Not a Folk worker thread>";
    }
    return [$cc compile]
}}]

Wish the web server handles route "/threads" with handler [list apply {{threadMonitorLib} {
    set pid [pid]
    set tids [glob -tails -directory /proc/$pid/task *]

    set userStacks [dict create]
    try {
        set euStackOutput [exec eu-stack --pid=[pid] --verbose]
        set currentTid none
        foreach line [split $euStackOutput "\n"] {
            if {[regexp {TID ([0-9]+):} $line -> tid]} {
                set currentTid $tid
            } else {
                dict set userStacks $currentTid \
                    "[dict getdef $userStacks $currentTid {}]\n$line"
            }
        }
    } on error e {}

    html [subst {
        <html>
        <head>
        <link rel="stylesheet" href="/style.css">
        <title>Threads</title>
        </head>

        <body>

        <h2>Threads</h2>

        <ol>
        [join [lmap tid $tids {
            set taskdir /proc/$pid/task/$tid

            set statusFd [open $taskdir/status r]
            set status [read $statusFd]; close $statusFd

            set workerInfo [$threadMonitorLib workerInfo $tid]

            try {
                set userStack [dict get $userStacks $tid]
            } on error e {
                set userStack "<not found>"
            }
            try {
                set stackFd [open $taskdir/stack r]
                set kernelStack [read $stackFd]; close $stackFd
            } on error e {
                set kernelStack "<not permitted>"
            }

            subst {<li style="[expr {$workerInfo eq "<Not a Folk worker thread>" ? "color: gray" : ""}]">
                $taskdir:<br>
                  [regexp -inline {State:[^\n]*\n} $status]<br>
                [htmlEscape $workerInfo]<br>

                <details><summary>User stack:</summary><pre>[htmlEscape $userStack]</pre></details>
                <details><summary>Kernel stack:</summary><pre>[htmlEscape $kernelStack]</pre></details>
            </li>}
        }] "\n"]
        </ol>

        </body>
        </html>
    }]
}} $threadMonitorLib]
