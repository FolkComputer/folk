When the hold save directory is /holdDirectory/ &\
     the program save directory is /programDirectory/ &\
     saving is ready {
    # are we already on the new format?
    if {[llength [glob -nocomplain $::env(HOME)/folk-data/hold/*]] > 0} {
        Claim the migration is complete
        return
    }

    puts "### MIGRATING ###"

    proc tryReadFile {filename} {
        set contents ""

        try {
            set fd [open $filename rb]
            set contents [read $fd]
            close $fd
        } on error e {}

        return $contents
    }

    # copy programs to new location
    exec sh << "cp ~/folk-printed-programs/* ~/folk-data/program/"

    # grab geometry before deleting
    set defaultGeom [tryReadFile "$::env(HOME)/folk-data/program/default.folkgeom"]
    if {$defaultGeom != ""} {
        Hold! -save -on calibration -key default-program-geometry \
            Claim the default program geometry is $defaultGeom
    }
    exec rm "$::env(HOME)/folk-data/program/default.folkgeom"
    # no need to save next-id, as folk already checks that an id doesn't exist
    exec rm "$::env(HOME)/folk-data/program/next-id.txt"

    # transfer calibration
    set calibrationPoses [tryReadFile "$::env(HOME)/folk-live/folk-calibration-poses.txt"]
    set calibration [tryReadFile "$::env(HOME)/folk-live/folk-calibration-output.txt"]
    if {$calibrationPoses != "" && $calibration != ""} {
        Hold! -save -on calibration -key calibration \
            Claim the calibration is $calibration
        Hold! -save -on calibration -key poses \
            Claim the calibration poses are $calibrationPoses
    }

    puts "\n\n\n### MIGRATION SUCCESSFUL ###\n"
    puts "Note: folk-printed-programs, folk-live/folk-calibration-output.txt, and folk-live/folk-calibration-poses.txt\
    are no longer needed. Feel free to delete them once you've confirmed that everything works!"
    puts "\n############################\n\n\n"

    Claim the migration is complete
}