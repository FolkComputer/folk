Wish the web server handles route "/programs" with handler {
    fn emitHtmlForProgramList {programList label} {
        set prettyLabel [string map {- " "} $label]
        set prettyLabel [string totitle $prettyLabel]:
        set returnList [list "<details data-label='$label' data-count='[llength $programList]'><summary>$prettyLabel ([llength $programList])</summary>"]
        lappend returnList "<ul>"
        foreach item $programList {
            lappend returnList "<li><details><summary>[dict get $item programName]</summary><pre><code>[htmlEscape [dict get $item programCode]]</code></pre></details></li>"
        }
        lappend returnList "</ul>"
        lappend returnList "</details>"
        join $returnList
    }

    fn emitHtmlForPrograms {programs} {
        set vp [list]; # builtin programs
        set cp [list]; # core programs
        set wp [list]; # web programs
        set rp [list]; # real programs

        foreach match $programs {
            set programName [dict get $match programName]
            switch -glob $programName {
                "builtin-programs/*" {
                    lappend vp $match
                }
                "/home/*" {
                    lappend cp $match
                }
                "web-program-*" {
                    lappend wp $match
                }
                default {
                    lappend rp $match
                }
            }
        }

        return [join [list \
            [emitHtmlForProgramList $rp "real-programs"] \
            [emitHtmlForProgramList $wp "web-programs"] \
            [emitHtmlForProgramList $vp "builtin-programs"] \
            [emitHtmlForProgramList $cp "core-programs"] \
            [expr {[llength $rp] ?
                   "<h2>[llength $rp] [expr {[llength $rp] == 1 ? {program is} : {programs are}}] out.</h2>" :
                      "No real programs are out."}] \
            ] "\n"]
    }

    html [subst {
        <html>
        <head>
            <title>Running programs</title>
            <link rel="stylesheet" href="/style.css">
            <style>
                body {
                    font-family: math;
                }
                summary {
                    font-family: monospace;
                    font-size: 2em;
                }
            </style>
            <script src="/lib/folk.js"></script>
            <script>
              const folk = new FolkWS();
            </script>
        </head>
        <body>
        [HtmlWhen the collected results for [list /programName/ has program code /programCode/] \
                  are /programs/ {
            emitHtmlForPrograms $programs
        }]
        </body>
        </html>
    }]
}
